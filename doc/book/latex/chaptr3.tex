\chapter{nJexl Syntax in 3 Minutes}\label{intro-njexl}

{\LARGE W}ith some understanding on C,C++, Java, then it will be very easy for you to learn nJexl. The biggest syntactic difference between nJexl and other languages is that the ';' statement end character is optional. When we consider a nJexl program it can be defined as a collection of objects that communicate via invoking each others methods. 


\begin{section}{Building Blocks}

\begin{subsection}{Identifiers}
\index{identifiers}
nJexl is case-sensitive, which means identifier Hello and hello would have different meaning.
All nJexl components require names. Names used for objects, classes, variables and methods are called identifiers. 
A keyword cannot be used as an identifier and identifiers are case-sensitive. 
See here for a list of keywords. 

\end{subsection}


\begin{subsection}{Assignments}
\index{assignment}
Most basic syntax of nJexl is, like any other language : assignment.
\begin{lstlisting}[style=JexlStyle][float]
 a = 1 //  assigns local variable a to Integer 1
 b = 1.0 //assigns local variable a to b float 1.0
 c = 'Hello, nJexl' // assigns local variable c to String 'Hello, nJexl'
 d = "Hello, nJexl" ## same, strings are either single or double quoted 
 /* 
 assigns the *then* value of a to e, 
 subsequent change in a wont reflect in e 
 */
 e = a \end{lstlisting}

\end{subsection}

\begin{subsection}{Comments}
\index{comments}

See from the previous subsection "//" used as line comments. So is "\#\#".
Along with the multiline comment "/*" with  "*/" :

\end{subsection}


\begin{subsection}{Basic Types}

Basic types are :
\begin{lstlisting}[style=JexlStyle][float]
 a = 1 // Integer 
 b = 1.0 //  float 
 c = 'Hello, nJexl' // String 
 d = 1.0d ## Double 
 I = 1h // BigInteger 
 D = 1.0b // BigDecimal  
 tt = true // boolean 
 tf = false // boolean 
 null_literal = null // special null type
\end{lstlisting}

\end{subsection}

\begin{subsection}{Multiple Assignment}
nJexl supports multiple assignment. It has various usage:

\begin{lstlisting}[style=JexlStyle][float]
 a = 1 // Integer 
 b = 1.0 //  float 
 c = 'Hello, nJexl' // String 
 // instead, do this straight :
 #(a,b,c ) = [ 1 , 1.0 , 'Hello, nJexl' ]   
\end{lstlisting}
\end{subsection}
\end{section}

\begin{section}{operators}
\index{operators : basic}


\begin{subsection}{Arithmetic}

\begin{lstlisting}[style=JexlStyle][float]
a = 1 + 1 // addition : a <- 2
z = 1 - 1 // subtraction : z <- 0
m = 2 * 3 // multiply : m <- 6
d = 3.0 / 2.0 // divide d <- 1.5   
x = 2 ** 10 // Exponentiation x <- 1024
y = -x // negation, y <- -1024 
r = 3 % 2 // modulo, r <- 1 
r = 3 mod 2 // modulo, r <- 1    
a += 1 // increment and assign 
z -= 1 // decrement and assign 
\end{lstlisting}

\end{subsection}

\begin{subsection}{Logical}

\begin{lstlisting}[style=JexlStyle][float]
o = true or true // true , or operator 
o = true || true // same 
a = true and false // false , and operator 
a = true && false // false, and operator  
\end{lstlisting}

\end{subsection}


\begin{subsection}{Comparison}

\begin{lstlisting}[style=JexlStyle][float]
t = 10 < 20 // true, less than
t = 10 lt  20 // true , less than 
f = 10 > 20 // false, greater then 
f = 10 gt 20 // false, greater then 
t = 10 <= 10 // true, less than or equal to 
t = 10 le  10 // true , less than or equal to 
t = 10 >= 10 // true, greater then or equal to 
t = 10 ge 10 // true, greater then or equal to 
t = ( 10 == 10 ) // true, equal to 
t = ( 10 eq 10 ) // true, equal to 
f = ( 10 != 10 ) // false, not equal to 
f = ( 10 ne 10 ) // false, not equal to 
\end{lstlisting}

\end{subsection}

\end{section}


\begin{section}{Conditions}

People coming from any other language would find them trivial.

\begin{subsection}{IF}

\begin{lstlisting}[style=JexlStyle][float]
x = 10 
if ( x < 100 ){
   x = x**2
}
write(x) // writes back x to standard output : 100
\end{lstlisting}
\end{subsection}

\begin{subsection}{ELSE}
\begin{lstlisting}[style=JexlStyle][float]
x = 1000 
if ( x < 100 ){
   x = x**2
}else{
  x = x/10 
}
write(x) // writes back x to standard output : 100
\end{lstlisting}
\end{subsection}

\begin{subsection}{ELSE IF}
\begin{lstlisting}[style=JexlStyle][float]
x = 100 
if ( x < 10 ){
   x = x**2
} else if( x > 80  ){
  x = x/10 
} else {
   x = x/100 
}
write(x) // writes back x to standard output : 10
\end{lstlisting}
\end{subsection}

\begin{subsection}{GOTO}
\begin{lstlisting}[style=JexlStyle][float]
/* 
 In case anyone like to use GOTO
*/
i = 0 
goto #label false // wont go there 
goto #label (i == 0) // will go there 
i = 2 
write("This should be skipped")

#label
write("This should be printed")
goto #unconditional
#unconditional
return i 
\end{lstlisting}
\end{subsection}
\end{section}

\begin{section}{Loops}

\begin{subsection}{WHILE}

\begin{lstlisting}[style=JexlStyle][float]
i = 0 
while ( i < 42 ){
  write(i)
  i += 1
}
\end{lstlisting}

\end{subsection}

\begin{subsection}{FOR}

For can iterate over any iterable, 
in short, it can iterate over string, any collection of objects,
a list, a dictionary or a range.
That is the first type of for :

\begin{lstlisting}[style=JexlStyle][float]
for ( i : [0:42] ){ // [a:b] is a range type 
  write(i)
}
\end{lstlisting}
The result is the same as the while loop.
A standard way, from C/C++/Java is to write the same as in :
\begin{lstlisting}[style=JexlStyle][float]
for ( i = 0 ; i < 42 ; i+= 1  ){ // [a:b] is a range type 
  write(i)
}
\end{lstlisting}
\end{subsection}
\end{section}

\begin{section}{Functions}

\begin{subsection}{Defining}
Functions are defined using the $def$ keyword.
And they can be assigned to variables, if one may wish to.
\begin{lstlisting}[style=JexlStyle][float]
def count_to_num(num){
   for ( i : [0:num] ){
      write(i)
   }
}
// just assign it 
fp = count_to_num
\end{lstlisting}

One can obviously return a value from function :

\begin{lstlisting}[style=JexlStyle][float]
def say_something(word){
   return ( "Hello " + word ) 
}
\end{lstlisting}
\end{subsection}

\begin{subsection}{Calling}
Calling a function is trivial :

\begin{lstlisting}[style=JexlStyle][float]
// calls the function with parameter
count_to_num(42) 
// calls the function at the variable with parameter
fp(42) 
// calls and assigns the return value 
greeting = say_something("Homo Erectus!" ) 
\end{lstlisting}

\end{subsection}

\begin{subsection}{Global Variables}
As you would be knowing that the functions create their local scope, 
so if you want to use variables - they must be defined in global scope.
Every external variable is readonly to the local scope, but to write to it, use $var$ keyword.  

\begin{lstlisting}[style=JexlStyle][float]
var a = 0
x = 0 
def use_var(){
   a = 42 
   write(a) // prints 42 
   write(x) // prints 0, can access global 
   x = 42 
   write(x) // prints 42 
}
// call the method
use_var()
write(a) // global a, prints 42 
write(x) // local x, prints 0 still
\end{lstlisting}
The result would be :
\begin{lstlisting}
42
0
42
42
0
\end{lstlisting}
\end{subsection}

\end{section}

\begin{section}{Anonymous Function as a Function Parameter}
A basic idea about what it is can be found \href{https://en.wikipedia.org/wiki/Anonymous\_function}{here}.
As most of the Utility functions use a specific type of anonymous function, 
which is nick-named as "Anonymous Parameter" to a utility function.

\begin{subsection}{Why it is needed?}

Consider a simple problem of creating a list from an existing one, 
by modifying individual elements in some way. This comes under 
\href{https://en.wikipedia.org/wiki/Map\_(higher-order\_function)}{map} , but the idea can be shared much simply:

\begin{lstlisting}[style=JexlStyle][float]
l = list()
  for ( x : [0:n] ){
      l.add ( x * x ) 
  }
return l 
\end{lstlisting}
Observe that the block inside the *for loop* takes minimal two parameters, 
in case we write it like this :
\begin{lstlisting}[style=JexlStyle][float]
// return is not really required, last executed line is return 
def map(x){ x * x } 
l = list()
for ( x : [0:n] ){
      l.add ( map(x) ) 
}
return l 
\end{lstlisting}

Observe now that we can now create another function, lets call it list\_from\_list :

\begin{lstlisting}[style=JexlStyle][float]
def map(x){ x * x } 
def list_from_list(fp, old_list)
    l = list()
    for ( x : old_list ){
    // use the function *reference* which was passed
      l.add( fp(x) )   
      }
      return l
} 
list_from_list(map,[0:n]) // same as previous 2 implementations
\end{lstlisting}
The same can be achieved in a much sorter way, with this :
\begin{lstlisting}[style=JexlStyle][float]
list{ $*$ }([0:n])
\end{lstlisting}

The curious block construct after the list function is called anonymous (function) parameter, 
which takes over the map function. The loop stays implicit, and the result is equivalent 
from the other 3 examples. The explanation is as follows.
For an anonymous function parameters, there are 3 implicit guaranteed arguments :

\begin{enumerate}
\item{ \$ --> Signifies the item of the collection , we call it the $ITEM$ }
\item{ \$\$ --> The context, or the collection itself , we call it the $CONTEXT$} 
\item{ \_ --> The index of the item in the collection, we call it the $ID$ of iteration } 
\item{ Another case to case parameter is :
     \_\$\_ --> Signifies the partial result of the processing , we call it $PARTIAL$ }
\end{enumerate}

\end{subsection}

\begin{subsection}{Some Use Cases}
The data structure section would showcase some use cases. 
But we would use a utility function to showcase the use of this anonymous function.
Suppose there is this function $minmax$ which takes a collection and returns the (min,max) tuple.
In short :

\begin{lstlisting}[style=JexlStyle][float]
#(min,max) = minmax(1,10,-1,2,4,11)
write(min) // prints -1
write(max) // prints 11    
\end{lstlisting}
But now, I want to find the minimum and maximum by length of a list of strings.
To do so, there has to be a way to pass the comparison done by length.
That is easy :

\begin{lstlisting}[style=JexlStyle][float]
#(min,max) = minmax{
             size($.0) < size($.1) 
             }( "" , "aa" , "abc" , "aa", "bbbbb" )
write(min) // prints empty string 
write(max) // prints bbbbb    
\end{lstlisting}

\end{subsection}

\end{section}

\begin{section}{Available Data Structures}

\begin{subsection}{Range}
A range is basically an iterable, with start and end separated by colon : $[a:b]$.
We already have seen this in action. "a" is inclusive while "b" is exclusive, 
this was designed the standard for loop in mind. There can also be an optional spacing parameter "s",
thus the range type in general is $[a:b:s]$, as described below:

\begin{lstlisting}[style=JexlStyle][float]
/* 
  when r = [a:b:s] 
  the equivalent for loop is :
  for ( i = a ; i < b ; i+= s ){
    ... body now ...  
  }
*/
r1 = [0:10] // a range from 0 to 9 with default spacing 1
//a range from 1 to 9 with spacing 2
r2 = [1:10:2] //1,3,5,7,9   
\end{lstlisting}
\end{subsection}

\begin{subsection}{Array}
A very simple way to generate inline array is this:

\begin{lstlisting}[style=JexlStyle][float]
a1 = [0 , 1, 2, 3 ] // an integer array 
a2 = [1 , 2.0 , 3, 4 ] // a number array 
ao = [ 0 , 1, 'hi', 34.5 ] // an object array   
AO = array ( 0,1,2,3 ) // an object array  
\end{lstlisting}
Arrays are not modifiable, you can not add or remove items in an array.
But, you can replace them :
\begin{lstlisting}[style=JexlStyle][float]
a1[0] = 42 // now a1 : [ 42, 1, 2, 3 ]
\end{lstlisting}
\end{subsection}

\begin{subsection}{List}
To solve the problem of adding and deleting item from an array, list were invented.
\begin{lstlisting}[style=JexlStyle][float]
l = list ( 0,1,2,3 ) // a list  
l += 10 // now the list is : 0,1,2,3,10
l -= 0 // now the list is : 1,2,3,10
x = l[0] // x is 1 now
l[1] = 100 // now the list is : 1,100,3,10 
\end{lstlisting}

\end{subsection}

\begin{subsection}{Set}
A set is a list such that the elements do not repeat.
Thus :

\begin{lstlisting}[style=JexlStyle][float]
// now the set is : 0,1,2,3
s = set ( 0,1,2,3,1,2,3 ) // a set  
s += 5 // now the set is : 0,1,2,3,5 
s -= 0 // now the set is : 1,2,3
\end{lstlisting}
\end{subsection}

\begin{subsection}{Dict}
A dictionary is a collection ( a list of ) (key,value) pairs.
The keys are unique, they are the $keySet()$. Here is how one defines a dict:

\begin{lstlisting}[style=JexlStyle][float]
d1 = { 'a' : 1 , 'b' : 2 } // a dictionary   
d2 = dict( ['a','b'] , [1,2] ) // same dictionary  
x = d1['a'] // x is 1
x = d1.a // x is 1 
d1.a = 10 // now d1['a'] --> 10 
\end{lstlisting}

\end{subsection}

\end{section}

